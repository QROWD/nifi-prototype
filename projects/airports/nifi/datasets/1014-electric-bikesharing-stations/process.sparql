PREFIX wgs: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX q: <http://qrowd-project.eu/ontology/>
CONSTRUCT {
  ?s
    a q:BikeStation ;
    q:id ?id ;
    rdfs:label ?name ;
    q:address ?address ;
    q:bikes ?bikes ;
    q:slots ?slots ;
    q:totalSlots ?totalSlots ;
    wgs:long ?x ;
    wgs:lat ?y ;
    .
}
{
  <https://os.smartcommunitylab.it/core.mobility/bikesharing/trento> url:text ?src .
  BIND(STRDT(?src, xsd:json) AS ?json) .

  # TODO the index var ?foo should be optional
  ?json json:unnest (?i ?foo) .

  BIND("http://qrowd-project.eu/resource/" AS ?ns)

  BIND(json:path(?i, "$.id") AS ?id)
  BIND(URI(CONCAT(?ns, ENCODE_FOR_URI(?id))) AS ?s)
  BIND(json:path(?i, "$.name") AS ?name)
  BIND(json:path(?i, "$.address") AS ?address)
  BIND(json:path(?i, "$.bikes") AS ?bikes)
  BIND(json:path(?i, "$.slots") AS ?slots)
  BIND(json:path(?i, "$.totalSlots") AS ?totalSlots)
  BIND(json:path(?i, "$.position[0]") AS ?x)
  BIND(json:path(?i, "$.position[1]") AS ?y)
}

